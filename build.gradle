buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven {
        name 'Packet Loss Gaming Repository'
        url 'https://mvn.packetloss.gg/'
    }
    maven {
        name 'Paper Repository'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'Spigot Repository'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name 'sk89q Repository'
        url 'https://maven.enginehub.org/repo/'
    }
    maven {
        name 'md_5 Repository'
        url 'https://repo.md-5.net/content/repositories/public/'
    }
    maven {
        name 'Comphenix Repository'
        url 'https://repo.comphenix.net/content/groups/public/'
    }
    maven {
        name 'Vault Repository'
        url 'https://nexus.theyeticave.net/content/repositories/pub_releases'
    }
    maven {
        name 'Protocol Lib Repository'
        url 'https://repo.dmulloy2.net/nexus/repository/public/'
    }
    maven {
        name 'minebench-repo'
        url 'https://repo.minebench.de/'
    }
}

dependencies {
    compileOnly group: 'com.garbagemule', name: 'mobarena', version: '0.104.3-SNAPSHOT'
    compileOnly group: 'gg.packetloss', name: 'Pitfall', version: '2.1.2'

    implementation group: 'gg.packetloss', name: 'OSBL', version: '1.1'
    implementation group: 'gg.packetloss', name: 'bukkit-text', version: '1.16.1.0'

    compileOnly group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.1-R0.1-SNAPSHOT'
    compileOnly group: 'com.destroystokyo.paper', name: 'paper', version: '1.16.1-R0.1-SNAPSHOT'

    compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '7.2.0-SNAPSHOT'
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard-legacy', version: '7.0.0-SNAPSHOT'
    compileOnly(group: 'com.sk89q', name: 'craftbook', version: '3.10.2-SNAPSHOT') {
        exclude group: 'com.sk89q', module: 'worldguard'
        exclude group: 'org.spigotmc', module: 'spigot-api'
        exclude group: 'org.spigotmc', module: 'spigot'
    }

    compileOnly group: 'com.sk89q', name: 'commandbook', version: '3.0-SNAPSHOT'
    compileOnly group: 'com.zachsthings.libcomponents', name: 'libcomponents-bukkit', version: '1.3.0-SNAPSHOT'

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.5.0"

    compileOnly group: 'net.milkbowl.vault', name: 'Vault', version: '1.5.6'
    compileOnly group: 'fr.neatmonster', name: 'nocheatplus', version: '3.13.7-RC'
    compileOnly(group: 'de.diddiz', name: 'logblock', version: '1.15.1.1-SNAPSHOT') {
        exclude group: 'com.sk89q', module: 'worldedit'
    }

    implementation group: 'net.sf.opencsv', name: 'opencsv', version: '2.0'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.1'

    compileOnly 'com.sk89q.worldedit.worldedit-libs:ap:7.2.0-SNAPSHOT'
    annotationProcessor 'com.sk89q.worldedit.worldedit-libs:ap:7.2.0-SNAPSHOT'
    annotationProcessor "com.google.guava:guava:21.0"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add("-Aarg.name.key.prefix=")
}

group = 'gg.packetloss'
version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

artifacts {
    archives shadowJar
}

shadowJar {
    dependencies {
        relocate ("com.skelril.OSBL", "gg.packetloss.grindstone.shaded.OSBL") {
            include(dependency("gg.packetloss:OSBL"))
        }
        relocate ("com.zaxxer.hikari", "gg.packetloss.grindstone.shaded.hikari") {
            include(dependency("com.zaxxer:HikariCP"))
        }
        relocate ("au.com.bytecode.opencsv", "gg.packetloss.grindstone.shaded.opencsv") {
            include(dependency('net.sf.opencsv:opencsv'))
        }
        relocate ("gg.packetloss.bukkittext", "gg.packetloss.grindstone.shaded.bukkittext") {
            include(dependency("gg.packetloss:bukkit-text"))
        }
    }
}